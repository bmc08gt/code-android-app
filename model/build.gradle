import org.apache.tools.ant.taskdefs.condition.Os

plugins {
    id 'java-library'
    id 'com.google.protobuf'
    id 'maven-publish'
}

def archSuffix = Os.isFamily(Os.FAMILY_MAC) ? ':osx-x86_64' : ''

version = '0.0.1'
group = 'com.codeinc.gen'

dependencies {
    implementation(Libs.grpc_protobuf_lite)
    implementation(Libs.grpc_stub)

    // Kotlin Generation
    implementation(Libs.grpc_kotlin)
    implementation(Libs.protobuf_kotlin_lite)
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:1.8.0"

}

def generatedSrcRoot = file("${buildDir}/generated/source/proto/main/java")
def generatedSrcRoot2 = file("${buildDir}/generated/source/proto/main/grpc")
def generatedSrcRoot3 = file("${buildDir}/generated/source/proto/main/grpcKt")
sourceSets.main {
    proto {
        srcDir "${rootDir}/model/proto"
    }
    java {
        srcDirs = ["$generatedSrcRoot", "$generatedSrcRoot2", "$generatedSrcRoot3"]
    }
}

compileJava {
    options.annotationProcessorGeneratedSourcesDirectory generatedSrcRoot
}

protobuf {
    protoc {
        artifact = "com.google.protobuf:protoc:${Versions.protobuf}$archSuffix"
    }
    plugins {
        grpc {
            artifact = "io.grpc:protoc-gen-grpc-java:${Versions.grpc}"
        }
//        javapgv {
//            artifact = "io.envoyproxy.protoc-gen-validate:protoc-gen-validate:${validateVersion}"
//        }
        grpcKt {
            artifact = "io.grpc:protoc-gen-grpc-kotlin:1.0.0:jdk7@jar"
        }
    }
    generateProtoTasks {
        all().each { task ->
            task.builtins {
                java {
                    option "lite"
                }
            }
        }
        all()*.plugins {
            grpc {
                option 'lite'
            }
            grpcKt {
                option 'lite'
            }
        }
    }
}

jar {
    manifest {
        attributes('code-api': project.name,
                'Version': project.version)
    }

    exclude "**/*agora*/**"
}

task sourcesJar(type: Jar) {
    from sourceSets.main.allSource
    archiveClassifier = 'sources'
    from generatedSrcRoot, generatedSrcRoot2, generatedSrcRoot3
    exclude "**/*agora*/**"
}

artifacts {
    archives sourcesJar
}

java {
    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8
}
